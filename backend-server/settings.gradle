pluginManagement {
  repositories {
    gradlePluginPortal()
  }
  resolutionStrategy {
    eachPlugin {
      if (requested.id.id == "org.jetbrains.kotlin.jvm") {
        useVersion "${kotlinVersion}"
      }
      if (requested.id.id == "org.jetbrains.kotlin.plugin.spring") {
        useVersion "${kotlinVersion}"
      }
    }
  }
  plugins {
    id 'io.spring.dependency-management' version "${spring_dependency_management_version}"
    id 'org.springframework.boot' version "${spring_boot_version}"
    id 'com.github.jakemarsden.git-hooks' version "${git_hook_plugin_version}"
    id 'org.owasp.dependencycheck' version "${dependency_check_plugin_version}"
    id 'com.bmuschko.docker-remote-api' version "${gradle_docker_plugin_version}"
    id 'nu.studer.credentials' version '2.1'
    id 'org.checkerframework' version '0.5.12'
    id "io.freefair.maven-optional" version "5.3.0"
    id "com.diffplug.spotless" version "5.9.0"
    id 'com.google.protobuf' version "${gradle_protobuf_plugin_version}"
  }
}

rootProject.name = 'backend'

include 'code-coverage-report'

include 'system-config'
include 'vikadata-clock'
include 'vikadata-core'
include 'vikadata-define'
include 'vikadata-boot-autoconfigure'

include 'vikadata-service:vikadata-db-model'
include 'vikadata-service:vikadata-mongodb-model'
include 'vikadata-service:vikadata-service-api'

include 'vikadata-project:api-service'
include 'vikadata-project:bill-scheduler'
include 'vikadata-project:space-scheduler'
include 'vikadata-project:social-service-dingtalk'
include 'vikadata-project:private-aider-service'

include 'vikadata-scheduler:scheduler-space'
include 'vikadata-scheduler:scheduler-bill'

file("${rootDir}/vikadata-social").eachDirMatch(~/vikadata-social.*/) {
  include "vikadata-social:${it.name}"
}

file("${rootDir}/vikadata-starters").eachDirMatch(~/vikadata-starter.*/) {
  include "vikadata-starters:${it.name}"
}

file("${rootDir}/vikadata-integration").eachDirMatch(~/vikadata-integration.*/) {
  include "vikadata-integration:${it.name}"
}

file("${rootDir}/vikadata-connector").eachDirMatch(~/vikadata-connector.*/) {
  include "vikadata-connector:${it.name}"
}