import java.nio.charset.StandardCharsets
import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
  id 'java'
  id 'org.springframework.boot'
  id "com.gorylenko.gradle-git-properties"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

// 打包环境
def profile = System.getProperty("profile") ?: "local"

System.out.println("Build Profile:" + profile)
dependencies {
  implementation project(':vikadata-define')
  implementation project(':vikadata-core')
  implementation project(':vikadata-service:vikadata-db-model')
  
  annotationProcessor rootProject.ext.dependencies['configuration-processor']
  
  implementation rootProject.ext.dependencies['spring-web-starter']
  implementation rootProject.ext.dependencies['mybatis-plus-starter']
  runtimeOnly rootProject.ext.dependencies['mysql']
  implementation rootProject.ext.dependencies['hutool']
  annotationProcessor rootProject.ext.dependencies['lombok']
  compileOnly rootProject.ext.dependencies['lombok']
  
  testCompileOnly rootProject.ext.dependencies["lombok"]
  testAnnotationProcessor rootProject.ext.dependencies["lombok"]
  testImplementation rootProject.ext.dependencies['spring-test-starter']
}


compileJava.dependsOn processResources
bootJar.dependsOn processResources

// Compile Java Setting
tasks.withType(JavaCompile) {
  options.encoding = StandardCharsets.UTF_8
  options.warnings = false
  options.compilerArgs += '-Xlint:-deprecation'
}

// Java Plugin On Gradle
bootJar {
  manifest {
    attributes 'Start-Class': 'com.vikadata.aider.AiderApplication'
    attributes 'Implementation-Version': archiveVersion
    attributes 'Built-Gradle': gradle.gradleVersion
    attributes 'Build-OS': System.getProperty("os.name")
    attributes 'Built-By': System.getProperty("user.name")
    attributes 'Build-Jdk': System.getProperty("java.version")
    attributes 'Build-Timestamp': LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyy-MM-dd HH:mm:ss"))
  }
}

// SpringBoot Plugin On Gradle
springBoot {
  mainClass = 'com.vikadata.aider.AiderApplication'
  // build-info.properties in resources/main/META-INF directory
  buildInfo {
    properties {
      artifact = 'Private Aider Server'
      version = rootProject.version
      group = rootProject.group
      name = 'Private Aider Server'
      version = '0.9.2-' + profile + '.' + (System.getenv().containsKey('CI_PIPELINE_ID') ? System.getenv('CI_PIPELINE_ID') : 'NoPipeline')
    }
  }
}